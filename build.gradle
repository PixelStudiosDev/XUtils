plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'dev.pixelstudios'
version = '1.10.8'

repositories {
    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url 'https://repo.oraxen.com/releases' }
    maven { url 'https://repo.auxilor.io/repository/maven-public/' }
    maven { url 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    maven { url 'https://repo.momirealms.net/releases/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.7-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'de.tr7zw:item-nbt-api:2.15.0'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT'
    compileOnly 'io.lumine:MythicLib-dist:1.6.2-SNAPSHOT'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14'
    compileOnly 'io.th0rgal:oraxen:1.181.0'
    compileOnly 'com.willfp:eco:6.74.1'

    compileOnly 'com.sk89q:worldguard:6.1.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'

    api 'com.github.cryptomorin:XSeries:13.3.3'

    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

shadowJar {
    exclude 'META-INF/**'
    minimize()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand version: version
    }
}

tasks.register('printVersion') {
    doLast {
        println version
    }
}